/**
 * ChaiLove: Testing Framework
 */
global testNames = [
	"graphics_print",
	"graphics_draw",
	"graphics_point",
	"mouse_getPosition",
	"graphics_print_font",
	"filesystem_getDirectoryItems"
]
global tests = []
global currenttest = 0
global defaultFont

def conf(t) {
	t.window.width = 460
	t.window.height = 320
}

def load() {
	// Index the test cases.
	for (name : testNames) {
		filesystem.load(name + ".chai");
		tests.push_back(eval(name + "()"));
	}

	// Load all the tests.
	for (test : tests) {
		test.load();
	}

	// Engage the default fault.
	defaultFont = graphics.newFont()
}

def draw() {
	tests[currenttest].draw();

	graphics.setFont(defaultFont)
	graphics.setColor(255, 255, 255)
	graphics.print("Test " + to_string(currenttest + 1) + "/" + to_string(tests.size()) + ": " + tests[currenttest].name, 5, 5);
	graphics.print("FPS " + to_string(timer.getFPS()), 5, 20);
}

def update(delta) {
	tests[currenttest].update(delta)
}

def joystickpressed(joystick, button) {
	if (button == "left" || button == "down") {
		currenttest = currenttest - 1
		if (currenttest < 0) {
			currenttest = tests.size() - 1
		}
	}
	else if (button == "right" || button == "up") {
		currenttest = currenttest + 1
		if (currenttest >= tests.size()) {
			currenttest = 0
		}
	}
}
